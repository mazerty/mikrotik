# change default admin password
/user set admin password=changeme

# a short delay to wait until all interfaces are up
:delay 10s

# a minimal working ethernet configuration over ip
/interface bridge add name=bridge
/interface bridge port add bridge=bridge interface=ether2
/interface bridge port add bridge=bridge interface=ether3
/interface bridge port add bridge=bridge interface=ether4
/interface bridge port add bridge=bridge interface=ether5
/ip dhcp-server network add address=192.168.3.0/24 gateway=192.168.3.1 dns-server=192.168.3.1 domain=lan
/ip pool add name=pool ranges=192.168.3.100-192.168.3.200
/ip dhcp-server add name=dhcp address-pool=pool interface=bridge disabled=no
/ip address add address=192.168.3.1/24 interface=bridge

# firewall rules to protect both router and lan before setting up ether1 and wlan
/ip firewall nat add chain=srcnat action=masquerade out-interface=ether1
/ip firewall filter add chain=input action=accept connection-state=established,related,untracked
/ip firewall filter add chain=input action=accept connection-state=new in-interface=bridge
/ip firewall filter add chain=input action=accept protocol=icmp
/ip firewall filter add chain=input action=log disabled=yes
/ip firewall filter add chain=input action=drop
/ip firewall filter add chain=forward action=fasttrack-connection connection-state=established,related
/ip firewall filter add chain=forward action=accept connection-state=established,related,untracked
/ip firewall filter add chain=forward action=accept connection-state=new in-interface=bridge
/ip firewall filter add chain=forward action=accept connection-state=new in-interface=ether1 connection-nat-state=dstnat
/ip firewall filter add chain=forward action=log disabled=yes
/ip firewall filter add chain=forward action=drop

# some more security restrictions
/ip neighbor discovery-settings set discover-interface-list=none
/tool mac-server set allowed-interface-list=none
/tool mac-server mac-winbox set allowed-interface-list=none

# setting up ether1 and outbound connectivity
/ip route add dst-address=0.0.0.0/0 gateway=192.168.1.254
/ip dns set allow-remote-requests=yes servers=8.8.8.8,8.8.4.4
/ip address add address=192.168.1.3/24 interface=ether1

# wireless configuration
/interface bridge port add bridge=bridge interface=wlan2
/interface wireless security-profiles set default mode=dynamic-keys authentication-types=wpa2-psk wpa2-pre-shared-key=changeme
/interface wireless set wlan2 mode=ap-bridge band=5ghz-a/n/ac wireless-protocol=802.11 channel-width=20/40/80mhz-Ceee frequency=auto ssid=changeme disabled=no

# static dhcp leases for port forwarding
/ip dhcp-server lease add mac-address=2C:56:DC:98:01:8C address=192.168.3.51
/ip dhcp-server lease add mac-address=28:C6:3F:4C:75:E8 address=192.168.3.52
/ip dhcp-server lease add mac-address=5C:51:4F:13:86:2A address=192.168.3.53

# destination nat rules for port forwarding
/ip firewall nat add chain=dstnat action=dst-nat protocol=tcp dst-port=30001 to-address=192.168.3.51 to-port=30001
/ip firewall nat add chain=dstnat action=dst-nat protocol=udp dst-port=30001 to-address=192.168.3.51 to-port=30001
/ip firewall nat add chain=dstnat action=dst-nat protocol=tcp dst-port=30002 to-address=192.168.3.52 to-port=30002
/ip firewall nat add chain=dstnat action=dst-nat protocol=udp dst-port=30002 to-address=192.168.3.52 to-port=30002
/ip firewall nat add chain=dstnat action=dst-nat protocol=tcp dst-port=30003 to-address=192.168.3.53 to-port=30003
/ip firewall nat add chain=dstnat action=dst-nat protocol=udp dst-port=30003 to-address=192.168.3.53 to-port=30003

# static dns entries
/ip dns static add name=mabbox.bytel.fr address=192.168.1.254
/ip dns static add name=mikrotik.lan address=192.168.3.1

# now the shameful part: mikrotik router os doesn't automatically add local hostnames to the dns...
/ip dns static add name=akhilleus.lan address=192.168.3.51
/ip dns static add name=hektor.lan address=192.168.3.52
/ip dns static add name=aiolos.lan address=192.168.3.53
